prototype(Shel.Neos.Logs:View.ShowLogfile) < prototype(Neos.Fusion:Component) {
    filename = ${filename}
    content = ${content}
    entries = ${entries}

    levelColors = Neos.Fusion:DataStructure {
        DEBUG = '#bbb'
        ERROR = '#f55'
        INFO = '#55f'
        EMERGENCY = '#f55'
        NOTICE = '#fff'
        WARNING = '#f50'
        CRITICAL = '#f55'
        ALERT = '#f55'
    }

    renderer = afx`
        <style>
            {'
                .neos .table-wrap {
                    overflow: hidden;
                    height: calc(100vh - 300px + 2px);
                    border: 1px solid #333;
                }
                .neos .header-row {
                    position: relative;
                    display: block;
                    border-bottom: 1px solid #333;
                }
                .neos.neos-module table th:first-child,
                .neos.neos-module table td:first-child {
                    padding-left: 16px !important;
                }
                .neos.neos-module table .column__date {
                    width: 160px;
                    white-space: nowrap;
                }
                .neos.neos-module table .column__ip {
                    width: 120px;
                }
                .neos.neos-module table .column__type {
                    width: 120px;
                }
                .neos.neos-module table .column__details {
                    width: auto;
                }
                .neos.neos-module .table-body {
                    overflow: auto;
                    height: calc(100vh - 300px - 40px);
                    display: block;
                }
                .neos.neos-module table td {
                    line-height: 1.3em;
                    padding: 5px 16px;
                }
            '}
        </style>
        <div class="neos-content neos-container-fluid">
            <div class="neos-row-fluid">
                <h2>Log file {props.filename}</h2>
                <br/>
                <p @if.noEntries={!props.entries}>
                    {props.content}
                </p>
                <div class="table-wrap" @if.hasEntries={props.entries}>
                    <table>
                        <thead>
                            <tr class="header-row">
                                <th class="column__date">Date</th>
                                <th class="column__ip" @if.hasIp={props.entries[0].ip}>IP</th>
                                <th class="column__type">Type</th>
                                <th class="column__details">Details</th>
                            </tr>
                        </thead>
                        <tbody class="table-body">
                            <Neos.Fusion:Loop items={props.entries} itemName="entry">
                                <tr style={'color:' + props.levelColors[entry.level]}>
                                    <td class="column__date">{entry.date}</td>
                                    <td class="column__ip" @if.hasIp={entry.ip}>{entry.ip}</td>
                                    <td class="column__type">{entry.level}</td>
                                    <td class="column__details">{entry.message}</td>
                                </tr>
                            </Neos.Fusion:Loop>
                        </tbody>
                    </table>
                </div>
            </div>

            <div class="neos-footer">
                <a class="neos-button">
                    <Neos.Fusion:UriBuilder @path="attributes.href" action="index" />
                    {I18n.id('Shel.Neos.Logs:Modules:action.back').value('Back').translate()}
                </a>
            </div>
        </div>
    `
}
